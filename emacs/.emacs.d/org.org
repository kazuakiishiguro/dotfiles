#+TITLE: ORG-MODE
#+STARTUP: overview

#+begin_src emacs-lisp
(require 'org)
(require 'org-tempo)
#+end_src

** BASE SETTINGS

#+begin_src emacs-lisp
 (setq org-directory "~/org")
 (setq org-agenda-files '("~/org/todo.org"))
 (setq org-refile-targets '(("~/org/todo.org" :maxlevel . 3)))
 (setq org-archive-location "~/org/archive.org::* From %s")
 (setq org-todo-keywords
       '((sequence "TODO(t)" "NOTE(n)" "DONE(d)" "CANCEL(c)")))
 (setq org-log-done 'time)
 (setq org-src-fontify-natively t)
 (setq org-src-tab-acts-natively t)
 (setq org-edit-src-content-indentation 1)
 (setq org-export-with-smart-quotes t)
 (setq org-export-with-toc t)
 (setq org-hide-emphasis-markers t)
 ;; Activate speed keys
 (setq org-use-speed-commands t)
#+end_src

** LOAD BABEL LANGUAGES

#+begin_src emacs-lisp
 (org-babel-do-load-languages
  'org-babel-load-languages
  '((C . t)
    (emacs-lisp . t)
    (shell . t)
    (rust . t)
    (js . t)
    (latex . t)
    (python . t)
    (scheme .t)))
#+end_src

** STRUCTURE TEMPLATES

#+begin_src emacs-lisp
 (add-to-list 'org-structure-template-alist '("s" . "src"))
 (add-to-list 'org-structure-template-alist '("kr" . "src C"))
 (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
 (add-to-list 'org-structure-template-alist '("rs" . "src rust"))
 (add-to-list 'org-structure-template-alist '("sc" . "src scheme"))
 (add-to-list 'org-structure-template-alist '("tex" . "src latex"))
#+end_src

** CAPTURE TASK

#+begin_src emacs-lisp
 (defvar my/capture-last-title nil
   "Title from the last my/capture-file call, reused in template body.")

 (defun my/capture-file (dir prompt)
   "Prompt for a title, validate, and return a new .org file path in DIR.
Capitalizes first letter, replaces spaces with underscores.
Errors on empty input or if file already exists."
   (let* ((raw (read-string prompt))
          (trimmed (string-trim raw)))
     (when (string-empty-p trimmed)
       (user-error "Title cannot be empty"))
     (let* ((capitalized (concat (upcase (substring trimmed 0 1))
                                 (substring trimmed 1)))
            (filename (concat (replace-regexp-in-string " " "_" capitalized) ".org"))
            (path (expand-file-name filename dir)))
       (when (file-exists-p path)
         (user-error "File already exists: %s" path))
       (setq my/capture-last-title capitalized)
       path)))

 (defun my/capture-title ()
   "Return the title saved by my/capture-file."
   my/capture-last-title)

 (defun my/sync-reference-categories ()
   "Rebuild ~/org/.categories from FILETAGS across all org files."
   (when (and buffer-file-name
              (string-suffix-p ".org" buffer-file-name)
              (string-prefix-p (expand-file-name org-directory)
                               (expand-file-name buffer-file-name)))
     (let (tags)
       (dolist (f (directory-files-recursively org-directory "\\.org$"))
         (with-temp-buffer
           (insert-file-contents f nil 0 512)
           (when (re-search-forward "^#\\+TYPE:[ \t]*category" nil t)
             (cl-pushnew (file-name-base f) tags :test #'string=))))
       (with-temp-file (expand-file-name ".categories" org-directory)
         (insert (mapconcat #'identity (sort tags #'string<) "\n") "\n")))))

 (add-hook 'after-save-hook #'my/sync-reference-categories)

 (defun my/reference-categories ()
   "Read categories from ~/org/.categories for completing-read."
   (let* ((file (expand-file-name ".categories" org-directory))
          (cats (when (file-exists-p file)
                  (with-temp-buffer
                    (insert-file-contents file)
                    (split-string (buffer-string) "\n" t)))))
     (unless cats
       (user-error "No categories found. Create one with C-c c K first"))
     (completing-read "Category: " cats nil t)))

 (setq org-capture-templates
       '(("t" "TODO" entry (file+headline "~/org/todo.org" "CURRENT")
	  "* TODO %?\n %i\n %a")

	 ("n" "Note" plain (file (lambda ()
	   (my/capture-file "~/org" "Note title: ")))
	  "#+TITLE: %(my/capture-title)\n#+DATE: %U\n#+FILETAGS:\n\n[[file:Daily/%(format-time-string \"%%Y-%%m-%%d\").org][%(format-time-string \"%%Y-%%m-%%d\")]]\n\n%?")

	 ("r" "Reference" plain (file (lambda ()
	   (my/capture-file "~/org/References" "Reference title: ")))
	  "#+TITLE: %(my/capture-title)\n#+DATE: %U\n#+FILETAGS: :%(my/reference-categories):\n\n%?")

	 ("K" "Category" plain (file (lambda ()
	   (my/capture-file "~/org" "Category name: ")))
	  "#+TITLE: %(my/capture-title)\n#+DATE: %U\n#+TYPE: category\n\n%?")

	 ("c" "Clipping" plain (file (lambda ()
	   (my/capture-file "~/org/Clippings" "Clipping title: ")))
	  "#+TITLE: %(my/capture-title)\n#+DATE: %U\n#+FILETAGS: :Manuscripts:\n\nSource: %^{URL}\n\n%?")

	 ("w" "Weekly review" plain (file (lambda ()
	   (expand-file-name
	    (concat "Week" (format-time-string "%U") "_-_" (format-time-string "%b_%Y") ".org")
	    "~/org")))
	  "#+TITLE: Week%(format-time-string \"%%U\") - %(format-time-string \"%%b %%Y\")\n#+DATE: %U\n#+FILETAGS: :weekly:\n\n* What happened\n\n%?\n\n* What I learned\n\n* Next week\n")))
#+end_src

**** TASK ARCHIVE

#+begin_src emacs-lisp
(defun my/mark-done-and-archive ()
  "Mark the state of an org-mode item as DONE and archive it."
  (interactive)
  (org-todo 'done)
  (org-archive-subtree))

(define-key org-mode-map (kbd "C-c C-x C-s") 'my/mark-done-and-archive)
#+end_src

** KEYBINDINGS

#+begin_src emacs-lisp
 (defun my/org-daily-file ()
   "Open or create today's daily anchor file."
   (interactive)
   (let* ((dir (expand-file-name "Daily" org-directory))
	  (file (expand-file-name (format-time-string "%Y-%m-%d.org") dir)))
     (unless (file-exists-p dir) (make-directory dir t))
     (find-file file)
     (when (= (buffer-size) 0)
       (insert (format "#+TITLE: %s\n#+DATE: %s\n"
		       (format-time-string "%Y-%m-%d")
		       (format-time-string "[%Y-%m-%d %a]"))))))

 (define-key global-map "\C-ca" 'org-agenda)
 (define-key global-map "\C-cc" 'org-capture)
 (define-key global-map "\C-cn" 'my/org-daily-file)
#+end_src

** EXPORTING
*** LATEX

#+begin_src emacs-lisp
 (require 'ox-latex)
#+end_src

*** DISABLE ASKING BEFORE EVALUATION

#+begin_src emacs-lisp
 (setq org-confirm-babel-evaluate nil)
#+end_src

*** HTMLIZE FOR ORG EXPORTS

#+begin_src emacs-lisp
 (use-package htmlize)
#+end_src

**** Footer configuration

#+begin_src emacs-lisp
(setq org-html-postamble nil)
#+end_src

*** ORG EXPORT FOR GITHUB MARKDOWN

#+begin_src emacs-lisp
 (use-package ox-gfm)
#+end_src

** PLUGINS

 #+begin_src emacs-lisp
  (use-package org-bullets
    :config
    (add-hook 'org-mode-hook (lambda () (org-bullets-mode 1))))
 #+end_src

** HOOKS
*** IN-BUFFER SETTING

#+begin_src emacs-lisp
 (defun my/org-mode-auto-titles ()
   (when (and (stringp buffer-file-name)
	      (string-match "\\.org$" buffer-file-name))
     (let ((title (file-name-base buffer-file-name)))
       (if (string-match "\\`[0-9]\\{4\\}-[0-9]\\{2\\}-[0-9]\\{2\\}-.*\\'" title)
	   (progn
	     (insert (concat "#+TITLE: " (replace-regexp-in-string "\\`[0-9]\\{4\\}-[0-9]\\{2\\}-[0-9]\\{2\\}-" "" title) "\n"))
	     (insert "#+LAYOUT: post\n")
	     (insert "#+OPTIONS: f:t\n"))))))
#+end_src

#+begin_src emacs-lisp
 (add-hook 'find-file-hook 'my/org-mode-auto-titles)
#+end_src

*** AUTO FILL MODE

#+begin_src emacs-lisp
 (add-hook 'org-mode-hook 'auto-fill-mode)
#+end_src
